pkgname=pkinittools
pkgver=latest
pkgrel=1
url="https://github.com/dirkjanm/${pkgname}.git"
arch=(any)
license=(MIT)
depends=(python)
source=("git+${url}")
sha256sums=(SKIP)

package() {
  cd "${srcdir}/${pkgname}"
  mkdir -p "${pkgdir}"/opt/archpkgs/bin "${pkgdir}/opt/archpkgs/${pkgname}"
  python -m venv "${pkgdir}/opt/archpkgs/${pkgname}"
  cp ./*.py ./**/*.py "${pkgdir}/opt/archpkgs/${pkgname}/bin"
  declare -x VIRTUAL_ENV="${pkgdir}/opt/archpkgs/${pkgname}" PYTHONPATH="${pkgdir}/opt/archpkgs/${pkgname}" PATH="${pkgdir}/opt/archpkgs/${pkgname}/bin:$PATH"
  pip install -r ./requirements.txt
  for binary in ./*.py ./**/*.py; do
    cat << EOF > "${pkgdir}/opt/archpkgs/bin/${pkgname}-$(basename "${binary%%.py}")"
#!/bin/sh
set -eu
export VIRTUAL_ENV=/opt/archpkgs/${pkgname} PYTHONPATH=/opt/archpkgs/${pkgname} PATH="/opt/archpkgs/${pkgname}/bin:\$PATH"
exec python /opt/archpkgs/${pkgname}/bin/$(basename ${binary}) "\$@"
EOF
    chmod +x "${pkgdir}/opt/archpkgs/bin/${pkgname}-$(basename "${binary%%.py}")"
  done
}

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}
