pkgname=responder
pkgver=latest
pkgrel=1
url="https://github.com/lgandx/${pkgname}.git"
arch=(any)
license=(GPL3)
depends=(python)
source=("git+${url}")
sha256sums=(SKIP)

package() {
  cd "${srcdir}/${pkgname}"
  mkdir -p "${pkgdir}"/opt/archpkgs/bin "${pkgdir}/opt/archpkgs/${pkgname}"
  python -m venv "${pkgdir}/opt/archpkgs/${pkgname}"
  declare -x VIRTUAL_ENV="${pkgdir}/opt/archpkgs/${pkgname}" PYTHONPATH="${pkgdir}/opt/archpkgs/${pkgname}" PATH="${pkgdir}/opt/archpkgs/${pkgname}/bin:$PATH"
  sed -i 's|^netifaces==0\.10\.4$|netifaces==0.10.5|' ./requirements.txt  # 0.10.4 fails to install
  pip install -r ./requirements.txt
  ./certs/gen-self-signed-cert.sh
  cp -r . "${pkgdir}/opt/archpkgs/${pkgname}/"
  cat << EOF > "${pkgdir}/opt/archpkgs/bin/${pkgname}"
#!/bin/sh
set -eu
export VIRTUAL_ENV=/opt/archpkgs/${pkgname} PYTHONPATH=/opt/archpkgs/${pkgname} PATH="/opt/archpkgs/${pkgname}/bin:\$PATH"
exec python /opt/archpkgs/${pkgname}/Responder.py "\$@"
EOF
  chmod +x "${pkgdir}/opt/archpkgs/bin/${pkgname}"
  cat << EOF > "${pkgdir}/opt/archpkgs/bin/${pkgname}-dumphash"
#!/bin/sh
set -eu
export VIRTUAL_ENV=/opt/archpkgs/${pkgname} PYTHONPATH=/opt/archpkgs/${pkgname} PATH="/opt/archpkgs/${pkgname}/bin:\$PATH"
# script loads Responder.db from current workdir
cd /opt/archpkgs/${pkgname}
exec python ./DumpHash.py "\$@"
EOF
  chmod +x "${pkgdir}/opt/archpkgs/bin/${pkgname}-dumphash"
}

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}
