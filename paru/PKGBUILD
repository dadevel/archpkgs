# source: https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=paru
pkgname=paru
pkgver=latest
pkgrel=1
url="https://github.com/morganamilo/${pkgname}.git"
backup=(etc/paru.conf)
arch=(any)
license=(GPL3)
depends=(git pacman 'libalpm.so>=14-64')
makedepends=(cargo)
optdepends=('bat: colored pkgbuild printing' 'devtools: build in chroot and downloading pkgbuilds')
source=("git+${url}")
sha256sums=(SKIP)

prepare() {
  cd "${srcdir}/${pkgname}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's|host: ||p')"
}

build () {
  cd "${srcdir}/${pkgname}"
  cargo build --frozen --release --target-dir ./target
  ./scripts/mkmo locale/
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm755 ./target/release/paru "${pkgdir}/usr/bin/paru"
  install -Dm644 ./paru.conf "${pkgdir}/etc/paru.conf"
  install -Dm644 ./man/paru.8 "$pkgdir/usr/share/man/man8/paru.8"
  install -Dm644 ./man/paru.conf.5 "$pkgdir/usr/share/man/man5/paru.conf.5"
  install -Dm644 ./completions/bash "${pkgdir}/usr/share/bash-completion/completions/paru.bash"
  install -Dm644 ./completions/fish "${pkgdir}/usr/share/fish/vendor_completions.d/paru.fish"
  install -Dm644 ./completions/zsh "${pkgdir}/usr/share/zsh/site-functions/_paru"
  install -d "${pkgdir}/usr/share/"
  cp -r ./locale "${pkgdir}/usr/share/"
}

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}
