pkgname=bloodhound
pkgver=latest
pkgrel=1
url="https://github.com/bloodhoundad/${pkgname}.git"
arch=(any)
license=(GPL3)
depends=(alsa-lib gtk3 nodejs nss)
makedepends=(git npm)
source=("git+${url}")
sha256sums=(SKIP)

build() {
  cd "${srcdir}/${pkgname}"
  declare -x NODE_OPTIONS='--openssl-legacy-provider'
  npm install
  npm run build
}

package() {
  cd "${srcdir}/${pkgname}"
  mkdir -p "${pkgdir}"/opt/archpkgs/bin "${pkgdir}/opt/archpkgs/${pkgname}"
  cp -r ./BloodHound-linux-x64/. "${pkgdir}/opt/archpkgs/${pkgname}/"
  cat << EOF > "${pkgdir}/opt/archpkgs/bin/${pkgname}"
#!/bin/sh
set -eu
cd /opt/archpkgs/${pkgname}
exec ./BloodHound
EOF
  chmod +x "${pkgdir}/opt/archpkgs/bin/${pkgname}"
}

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}
