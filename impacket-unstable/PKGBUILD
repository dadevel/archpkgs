pkgname=impacket-unstable
pkgver=latest
pkgrel=1
# this fork merges PRs faster than https://github.com/fortra/impacket
url=https://github.com/theporgs/impacket.git
arch=(any)
license=(Apache)
depends=(python)
source=("git+${url}")
sha256sums=(SKIP)

package() {
    cd "${srcdir}/impacket"
    mkdir -p "${pkgdir}/opt/archpkgs/bin" "${pkgdir}/opt/archpkgs/${pkgname}"
    python -m venv "${pkgdir}/opt/archpkgs/${pkgname}"
    declare -x VIRTUAL_ENV="${pkgdir}/opt/archpkgs/${pkgname}" PYTHONPATH="${pkgdir}/opt/archpkgs/${pkgname}" PATH="${pkgdir}/opt/archpkgs/${pkgname}/bin:$PATH"
    pip install .
    for binary in ./examples/*.py; do
        binname="$(basename "${binary%%.py}")"
        binname="${binname,,}"
        binname="${binname//_/-}"
        cat << EOF > "${pkgdir}/opt/archpkgs/bin/impacket-unstable-${binname}"
#!/bin/sh
set -eu
export VIRTUAL_ENV=/opt/archpkgs/${pkgname} PYTHONPATH=/opt/archpkgs/${pkgname} PATH="/opt/archpkgs/${pkgname}/bin:\$PATH"
exec python /opt/archpkgs/${pkgname}/bin/$(basename "${binary}") "\$@"
EOF
        chmod +x "${pkgdir}/opt/archpkgs/bin/impacket-unstable-${binname}"
    done
}

pkgver() {
    cd "${srcdir}/impacket"
    echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}
